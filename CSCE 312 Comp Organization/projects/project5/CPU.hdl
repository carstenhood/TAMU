// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    // determine type of instruction
    Not(in=instruction[15], out=instructA);
    Not(in=instructA, out=instructC);
    
    // set result of C instruction to A
    And(a=instructC, b=instruction[5], out=setAtoALU);
    Mux16(a=instruction, b=outALU, sel=setAtoALU, out=inputA);
    
    // if A type OR a C type that sets to A, then load A register
    Or(a=instructA, b=setAtoALU, out=loadA);    
    ARegister(in=inputA, load=loadA, out=outA);
    
    // use A bit to select A or M
    Mux16(a=outA, b=inM, sel=instruction[12], out=outAM);   
    
    // load the D register from ALU
    And(a=instructC, b=instruction[4], out=loadD);
    DRegister(in=outALU, load=loadD, out=outD);  
    
    // perform ALU calculation
    ALU(x=outD, y=outAM, zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], f=instruction[7],
        no=instruction[6], out=outALU, zr=outZR, ng=outNG); 
        
    // set targets for writing to memory
    Or16(a=false, b=outA, out[0..14]=addressM);
    Or16(a=false, b=outALU, out=outM);
    And(a=instructC, b=instruction[3], out=writeM);
    
    // determine PC increment and calculation
    And(a=outZR, b=instruction[1], out=JEQ); // jump if zero
    And(a=outNG, b=instruction[2], out=JLT); // jump if less than zero
    Or(a=outZR, b=outNG, out=notpositive);
    Not(in=notpositive, out=positive);
    And(a=positive, b=instruction[0], out=JGT); // jump if greater than zero
    Or(a=JEQ, b=JLT, out=JLE);
    Or(a=JLE, b=JGT, out=jumpToA); // jump if a condition was met
    And(a=instructC, b=jumpToA, out=loadPC); // only jump if C type instruction
    Not(in=loadPC, out=incremPC); // increment if not jumping
    PC(in=outA, inc=incremPC, load=loadPC, reset=reset, out[0..14]=pc);
}